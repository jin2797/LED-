#include <Servo.h>

#define PIN_IR 0
#define PIN_LED 9
#define PIN_SERVO 10

#define _DUTY_MIN 500
#define _DUTY_NEU 1500
#define _DUTY_MAX 2500

#define _DIST_MIN 100.0
#define _DIST_MAX 250.0

#define EMA_ALPHA 0.30
#define LOOP_INTERVAL 25

#define LED_ON LOW
#define LED_OFF HIGH

Servo myservo;
unsigned long last_loop_time = 0;
float dist_prev = (_DIST_MIN + _DIST_MAX) * 0.5;
float dist_ema  = (_DIST_MIN + _DIST_MAX) * 0.5;

void setup() {
  pinMode(PIN_LED, OUTPUT);
  digitalWrite(PIN_LED, LED_OFF);
  myservo.attach(PIN_SERVO);
  myservo.writeMicroseconds(_DUTY_NEU);
  Serial.begin(1000000);
  last_loop_time = millis();
}

void loop() {
  unsigned long time_curr = millis();
  if (time_curr < (last_loop_time + LOOP_INTERVAL)) return;
  last_loop_time += LOOP_INTERVAL;

  int duty;
  float a_value, dist_raw;

  a_value = analogRead(PIN_IR);
  if (a_value <= 9.0) a_value = 10.0;
  dist_raw = ((6762.0 / (a_value - 9.0)) - 4.0) * 10.0;

  bool in_range = (dist_raw >= _DIST_MIN) && (dist_raw <= _DIST_MAX);
  if (in_range) {
    dist_prev = dist_raw;
    digitalWrite(PIN_LED, LED_ON);
  } else {
    dist_raw = dist_prev;
    digitalWrite(PIN_LED, LED_OFF);
  }

  dist_ema = EMA_ALPHA * dist_raw + (1.0 - EMA_ALPHA) * dist_ema;

  if (dist_ema < _DIST_MIN) dist_ema = _DIST_MIN;
  if (dist_ema > _DIST_MAX) dist_ema = _DIST_MAX;

  float ratio = (dist_ema - _DIST_MIN) / (_DIST_MAX - _DIST_MIN);
  duty = (int)(_DUTY_MIN + (_DUTY_MAX - _DUTY_MIN) * ratio);

  myservo.writeMicroseconds(duty);

  Serial.print("_DUTY_MIN:");  Serial.print(_DUTY_MIN);
  Serial.print("_DIST_MIN:");  Serial.print(_DIST_MIN);
  Serial.print(",IR:");        Serial.print(a_value);
  Serial.print(",dist_raw:");  Serial.print(dist_raw);
  Serial.print(",ema:");       Serial.print(dist_ema);
  Serial.print(",servo:");     Serial.print(duty);
  Serial.print(",_DIST_MAX:"); Serial.print(_DIST_MAX);
  Serial.print(",_DUTY_MAX:"); Serial.print(_DUTY_MAX);
  Serial.println("");
}
